program : definition
        | definition program

definition : varDef
           | functionDef

varDef : VAR ID DOT
       | VAR ID ASSIGN expression DOT

functionDef : DEF ID OPAREN optArgumentList CPAREN block

expression : unary
           | unary operator expression

operator : PLUS
         | MINUS
         | TIMES
         | DIVIDES
         | MODULUS
         | AND
         | OR
         | EQUALS
         | ASSIGN
         | GREATERTHAN
         | LESSTHAN

unary : idDef
      | INTEGER
      | BOOLEAN
      | CHARACTER
      | STRING
      | OPAREN expression CPAREN
      | PRINT OPAREN expression CPAREN
      | RETURN expression
      | NOT unary

idDef : ID
      | ID OBRACKET expression CBRACKET
      | ID OPAREN optExpressionList CPAREN

expressionList : expression
               | expression COMMA expressionList

optExpressionList : expressionList
                  | *empty*

argumentList : ID
             | ID COMMA argumentList

optArgumentList : argumentList
                | *empty*

block : START optStatementList STOP

optStatementList : statementList
                 | *empty*

statementList : statement
              | statement statementList

statement : expression DOT
          | ifStatement
          | whileLoop
          | definition

ifStatement : IF OPAREN expression CPAREN block optElse

optElse : notOptElse
        | *empty*

notOptElse : ELSE block
           | ELSE ifStatement

whileLoop : WHILE OPAREN expression CPAREN block


